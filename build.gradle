apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'checkstyle'
apply plugin: 'net.ltgt.errorprone'

version = '22.0'

repositories {
    mavenCentral()
    maven { url "http://repo.hortonworks.com/content/repositories/releases" }
    maven { url "http://repo.hortonworks.com/content/groups/public" }
    maven { url "http://oss.sonatype.org/content/groups/public/" }
    maven { url "https://plugins.gradle.org/m2/" }
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

checkstyle {
    configFile = file("$project.rootDir/config/checkstyle/checkstyle.xml")
    toolVersion = "8.22"
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
}

task createDirs() {
  file('build/package').mkdirs()
}

task buildWeb(type:Exec) {
    workingDir './web'
    commandLine './deploy.sh'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task copyWeb(type: Copy) {
  from('web/dist')
  into('build/package/web')
}

task copyDeps(type: Copy) {
  from(configurations.compile)
  into('build/package/lib')
}

task copyLibs(type: Copy, dependsOn: 'jar') {
  from('build/libs')
  into('build/package/lib')
}

task copyPackage(type: Copy) {
  from('src/package')
  into('build/package')
}

task copyData(type: Copy) {
    from('data')
    into('build/package/data')
}

task copyResult(type: Copy) {
    from('result')
    into('build/package/result')
}

task copy(dependsOn: [
      'createDirs',
      'buildWeb',
      'copyWeb',
      'copyDeps',
      'copyLibs',
      'copyPackage',
      'copyData',
      'copyResult']) {
  }

distributions {
  main {
    baseName = 'yanagishima'
    contents {
      from { 'build/package' }
    }
  }
}

distZip.dependsOn 'copy'


buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies { classpath "net.ltgt.gradle:gradle-errorprone-plugin:1.1.1" }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'org.slf4j:slf4j-log4j12:1.7.10'
    compile 'log4j:log4j:1.2.17'
    compile 'org.eclipse.jetty.aggregate:jetty-all:9.4.25.v20191220'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'com.facebook.presto:presto-client:0.215'
    compile 'io.prestosql:presto-client:313'
    compile 'io.prestosql:presto-parser:313'
    compile 'com.google.inject:guice:4.0'
    compile 'com.google.inject.extensions:guice-servlet:4.0'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'net.sf.jopt-simple:jopt-simple:4.8'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'org.apache.httpcomponents:fluent-hc:4.4.1'
    compile 'org.xerial:sqlite-jdbc:3.20.1'
    compile 'me.geso:tinyorm:1.13.0'
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    compile 'org.komamitsu:fluency:1.7.0'
    compile 'org.apache.hive:hive-jdbc:1.2.1000.2.5.3.0-37'
    compile 'org.apache.hadoop:hadoop-common:2.7.3.2.5.3.0-37'
    compile 'org.apache.commons:commons-csv:1.5'
    compile 'com.google.guava:guava:21.0'
    compile 'com.github.wyukawa.elasticsearch.unofficial.jdbc.driver:elasticsearch-jdbc-driver:0.0.9'
    compile 'org.jsoup:jsoup:1.11.3'
    compile 'mysql:mysql-connector-java:5.1.47'

    errorprone 'com.google.errorprone:error_prone_core:2.3.4'

    // For tests
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:2.23.4'
}
