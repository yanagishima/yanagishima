plugins {
    id 'java'
    id 'distribution'
    id 'checkstyle'
    id "net.ltgt.errorprone" version "2.0.1"
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.3.5.RELEASE'
}

version = '22.0'

repositories {
    mavenCentral()
    maven { url "https://repo.hortonworks.com/content/repositories/releases" }
    maven { url "https://repo.hortonworks.com/content/groups/public" }
    maven { url "https://plugins.gradle.org/m2/" }
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

checkstyle {
    configFile = file("$project.rootDir/config/checkstyle/checkstyle.xml")
    toolVersion = "8.29"
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
}

task createDirs() {
  file('build/package').mkdirs()
}

task buildWeb(type:Exec) {
    workingDir './web'
    commandLine './deploy.sh'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task copyWeb(type: Copy) {
  from('web/dist')
  into('build/package/web')
}

task copyDeps(type: Copy) {
  from(configurations.compile)
  into('build/package/lib')
}

task copyLibs(type: Copy, dependsOn: 'jar') {
  from('build/libs')
  into('build/package/lib')
}

task copyPackage(type: Copy) {
  from('src/package')
  into('build/package')
}

task copyConfig(type: Copy) {
  from('src/main/resources/config')
  into('build/package/config')
}

task copyData(type: Copy) {
    from('data')
    into('build/package/data')
}

task copyResult(type: Copy) {
    from('result')
    into('build/package/result')
}

task copy(dependsOn: [
      'createDirs',
      'buildWeb',
      'copyWeb',
      'copyDeps',
      'copyLibs',
      'copyPackage',
      'copyConfig',
      'copyData',
      'copyResult']) {
  }

distributions {
  main {
    baseName = 'yanagishima'
    contents {
      from { 'build/package' }
    }
  }
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        error('MissingOverride', 'ClassCanBeStatic', 'ArrayToString', 'RemoveUnusedImports', "PreferJavaTimeOverload")
        disable('JavaTimeDefaultTimeZone', 'UnusedVariable', 'UnusedMethod', 'FutureReturnValueIgnored', 'StringSplitter')
    }
}

distZip.dependsOn 'copy'


dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jetty'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'

    // MySQL
    implementation 'mysql:mysql-connector-java:5.1.47'

    // Logger
    implementation 'org.slf4j:slf4j-api:1.7.10'
    implementation 'org.slf4j:slf4j-log4j12:1.7.10'

    // Presto
    implementation 'io.prestosql:presto-client:347'
    implementation 'io.prestosql:presto-parser:347'
    implementation 'com.squareup.okhttp3:okhttp'

    // Spark
    implementation 'org.apache.httpcomponents:fluent-hc'
    implementation 'org.jsoup:jsoup:1.11.3'

    // Hive
    implementation 'org.apache.hive:hive-jdbc:1.2.1000.2.5.3.0-37'
    implementation 'org.apache.hadoop:hadoop-common:2.7.3.2.5.3.0-37'

    // Elasticsearch
    implementation 'com.github.wyukawa.elasticsearch.unofficial.jdbc.driver:elasticsearch-jdbc-driver:0.0.9'

    // Fluentd
    implementation 'org.komamitsu:fluency:1.7.0'

    // Utility
    implementation 'org.apache.commons:commons-csv:1.5'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'com.github.wyukawa.elasticsearch.unofficial.jdbc.driver:elasticsearch-jdbc-driver:0.0.9'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    // Errorprone
    errorprone 'com.google.errorprone:error_prone_core:2.3.4'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.assertj:assertj-core'
}

test {
    useJUnitPlatform()
}

configurations {
  all {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'com.google.code.gson', module: 'gson'
    exclude group: 'org.eclipse.jetty.aggregate', module: 'jetty-all'
    exclude group: 'javax.servlet', module: 'servlet-api'
  }
}
